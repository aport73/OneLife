plugins {
    id 'java'
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.19"
    id("xyz.jpenilla.run-paper") version "3.0.2"
}

version = '2-Beta-20'
repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly 'net.luckperms:api:5.5'
    //noinspection GrUnresolvedAccess
    paperweight.paperDevBundle("1.21.10-R0.1-SNAPSHOT")

    // https://mvnrepository.com/artifact/tools.jackson.core/jackson-core
    implementation 'tools.jackson.core:jackson-core:3.0.4'
    // https://mvnrepository.com/artifact/tools.jackson.core/jackson-databind
    implementation 'tools.jackson.core:jackson-databind:3.0.4'
    // https://mvnrepository.com/artifact/tools.jackson.dataformat/jackson-dataformat-yaml
    implementation 'tools.jackson.dataformat:jackson-dataformat-yaml:3.0.0'
}

tasks {
    runServer {
        //Download required plugins automatically
        downloadPlugins {
            url("https://download.luckperms.net/1605/bukkit/loader/LuckPerms-Bukkit-5.5.16.jar")
        }
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.10")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
